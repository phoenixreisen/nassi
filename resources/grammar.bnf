DIAGRAM     = STATEMENT [_ STATEMENT]*

<STATEMENT> = _ (INCLUDE | FOR | WHILE | UNTIL | SUB | IF | SWITCH | THROW | CATCH | TEXTSTMT) _

INCLUDE     = <'include'> _ FILENAME
FILENAME    = #"\".*\""

THROW       = <'throw'> _ ERRORCODE _ TEXT
FOR         = <'for'>   _ TEXT _ BLOCK
WHILE       = <'while'> _ TEXT _ BLOCK
UNTIL       = <'until'> _ TEXT _ BLOCK
SUB         = <'sub'>   _ TEXT _ BLOCK

IF          = <'if'> _ TEXT _ BLOCK [_ ELSE]
ELSE        = <'else'> _ BLOCK

CATCH       = <'catch'> _ <'{'> _ HANDLERS _ <'}'>
HANDLERS    = HANDLE [_ HANDLE]*
HANDLE      = <'handle'> _ ERRORCODEREF _ BLOCK

SWITCH      = <'switch'>  _ TEXT _ <'{'> _ CASES _ <'}'>
CASES       = (CASE | DEFAULT) [_ (CASE | DEFAULT)]*
CASE        = <'case'>    _  TEXT _  BLOCK
DEFAULT     = <'default'> _ [TEXT _] BLOCK

BLOCK       = <'{'> [_ STATEMENT]+ _ <'}'>
<_>         = <#"[\s]*">
ERRORCODE   = #"(#[A-Za-z][\.A-Za-z0-9_-]+)"
ERRORCODEREF = ERRORCODE
TEXTSTMT    = TEXT
<TEXT>      = PARAGRAPH / SENTENCE
SENTENCE    = #"[^\n^{^}]+" 
PARAGRAPH   = #"(?s)\"\"\".*?\"\"\""

