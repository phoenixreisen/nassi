DIAGRAM     = [METADATA] STATEMENT [_ STATEMENT]*

<_>         = <#"[\s]*">    (* optional whitespace *)
<__>        = <#"[\h]*">    (* optional horizontal whitespace *)

METADATA    = [METAINFO]*
METAINFO    = <';'> __ #"[A-Za-z][\.A-Za-z0-9_-]+" __ <'='> __ #".*" __ #"\r?\n"

<STATEMENT> = _ (PREAMBLE | EPILOGUE | INCLUDE | FOR | WHILE | UNTIL | SUB | IF | SWITCH | THROW | CATCH | TEXTSTMT) _

PREAMBLE    = <'preamble'> _ TEXT
EPILOGUE    = <'epilogue'> _ TEXT

INCLUDE     = <'include'> _ FILENAME
FILENAME    = #"\".*\""

THROW       = <'throw'> _ ERRORCODE _ TEXT
FOR         = <'for'>   _ TEXT _ BLOCK
WHILE       = <'while'> _ TEXT _ BLOCK
UNTIL       = <'until'> _ TEXT _ BLOCK
SUB         = <'sub'>   _ TEXT _ BLOCK

IF          = <'if'> _ TEXT _ BLOCK [_ ELSE]
ELSE        = <'else'> _ BLOCK

CATCH       = <'catch'> _ <'{'> _ HANDLERS _ <'}'>
HANDLERS    = HANDLE [_ HANDLE]*
HANDLE      = <'handle'> _ ERRORCODEREF _ BLOCK

SWITCH      = <'switch'>  _ TEXT _ <'{'> _ CASES _ <'}'>
CASES       = (CASE | DEFAULT) [_ (CASE | DEFAULT)]*
CASE        = <'case'>    _ TEXT _ BLOCK
DEFAULT     = <'default'> _ TEXT _ BLOCK

BLOCK       = <'{'> [_ STATEMENT]+ _ <'}'>
ERRORCODE   = #"(#[A-Za-z][\.A-Za-z0-9_-]+)"
ERRORCODEREF = ERRORCODE [_ ERRORCODE]*
TEXTSTMT    = TEXT
<TEXT>      = PARAGRAPH / SENTENCE
SENTENCE    = #"[^\n^{^}]+" 
PARAGRAPH   = #"(?s)\"\"\".*?\"\"\""
